apply plugin: 'com.android.application'
apply plugin: 'maven-publish'

android {
    compileSdkVersion versions.sdk.compile

    defaultConfig {
        applicationId "su.comp.bk"

        minSdkVersion versions.sdk.min
        targetSdkVersion versions.sdk.target

        versionCode versions.project.major * 1000000 + versions.project.minor * 1000 + versions.project.patch
        versionName versions.project.number

        buildConfigField "long", "BUILD_TIMESTAMP", System.currentTimeMillis() + "L"
    }

    // Test resources management
    sourceSets {
        test {
            resources.srcDirs += ['src/test/resources']
            resources.srcDirs += ['src/main/res/raw']
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    // Release management
    signingConfigs {
        release {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            keyAlias properties.getProperty('key.alias')
            keyPassword properties.getProperty('key.password')
            storeFile file(properties.getProperty('key.store.file'))
            storePassword properties.getProperty('key.store.password')
        }
    }
    buildTypes {
        debug {
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            signingConfig signingConfigs.release
        }
    }
}

// Release artifacts management
afterEvaluate {
    publishing {
        publications {
            releaseApk(MavenPublication) {
                from components.release_apk

                groupId 'com.github.3cky'
                artifactId 'bkemu-android'
                version versions.project.number

                artifact source: file('build/outputs/apk/release/app-release.apk'),
                        classifier: 'signed', extension: 'apk'
                artifact source: file('build/outputs/mapping/release/mapping.txt'),
                        classifier: 'mapping', extension: 'txt'
            }
        }
        repositories {
            maven {
                name "archive"
                url = layout.buildDirectory.dir('archive')
            }
        }
    }
}

dependencies {
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'

    implementation 'commons-lang:commons-lang:2.6'
    implementation 'com.samskivert:jmustache:1.9'
    implementation 'com.andkulikov:transitionseverywhere:2.0.0-alpha01'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'org.msgpack:jackson-dataformat-msgpack:0.9.1'

    testImplementation 'junit:junit:4.13.1'
    testImplementation 'commons-io:commons-io:2.4'
    testImplementation 'org.robolectric:robolectric:4.6'
    testImplementation 'net.lachlanmckee:timber-junit-rule:1.0.1'
}