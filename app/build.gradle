apply plugin: 'com.android.application'
apply plugin: 'maven-publish'

android {
    namespace 'su.comp.bk'

    defaultConfig {
        applicationId "su.comp.bk"

        minSdkVersion versions.sdk.min
        targetSdkVersion versions.sdk.target
        compileSdk versions.sdk.compile

        multiDexEnabled true

        versionCode versions.project.major * 1000000 + versions.project.minor * 1000 + versions.project.patch
        versionName versions.project.number

        buildFeatures {
            buildConfig = true
        }

        buildConfigField "long", "BUILD_TIMESTAMP", System.currentTimeMillis() + "L"
    }

    // Test resources management
    sourceSets {
        test {
            resources.srcDirs += ['src/test/resources']
            resources.srcDirs += ['src/main/res/raw']
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    // Release management
    signingConfigs {
        release {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            keyAlias properties.getProperty('key.alias')
            keyPassword properties.getProperty('key.password')
            storeFile file(properties.getProperty('key.store.file'))
            storePassword properties.getProperty('key.store.password')
        }
    }
    buildTypes {
        debug {
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            signingConfig signingConfigs.release
        }
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

// Release artifacts management
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId 'com.github.3cky'
                artifactId 'bkemu-android'
                version versions.project.number

                artifact source: file('build/outputs/apk/release/app-release.apk'),
                        classifier: 'signed', extension: 'apk'
                artifact source: file('build/outputs/mapping/release/mapping.txt'),
                        classifier: 'mapping', extension: 'txt'
            }
        }
        repositories {
            maven {
                name "archive"
                url = layout.buildDirectory.dir('archive')
            }
        }
    }
    publishReleasePublicationToMavenLocal.configure {
        mustRunAfter packageRelease
    }
}

dependencies {
    implementation project(':lib')

    api 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.android.material:material:1.12.0'

    implementation 'com.samskivert:jmustache:1.9'
    implementation 'org.msgpack:jackson-dataformat-msgpack:0.9.1'
    implementation 'com.github.hedzr:android-file-chooser:v1.2.0-final'

    implementation("org.slf4j:slf4j-api:2.0.13")
    implementation("com.jakewharton.timber:timber:5.0.1")
    implementation("io.github.unveloper:slf4j-timber:0.0.8")
}